//WOOJIN CHOI, 10/13/2025
//@version=6
indicator(title="FX_ATR", shorttitle="FXATR", overlay=false)

spread_chart(pair) =>
    spread = 0.0
    switch pair
        "AUDUSD" => spread := 1.1
        "EURUSD" => spread := 1.5
        "GBPUSD" => spread := 1.7
        "NZDUSD" => spread := 2.3
        "USDCAD" => spread := 1.6
        "USDCHF" => spread := 1.7
        "USDJPY" => spread := 1.5
        "AUDCAD" => spread := 2.3
        "AUDCHF" => spread := 1.8
        "AUDJPY" => spread := 2.2
        "AUDNZD" => spread := 2.8
        "CADCHF" => spread := 2.2 
        "CADJPY" => spread := 3.3
        "EURAUD" => spread := 3.7
        "EURCAD" => spread := 3.3
        "EURCHF" => spread := 2.2
        "EURGBP" => spread := 1.5
        "EURJPY" => spread := 2.5
        "EURNZD" => spread := 5.8
        "GBPAUD" => spread := 4.7
        "GBPCAD" => spread := 4.2
        "GBPCHF" => spread := 3.7
        "GBPJPY" => spread := 3.5
        "GBPNZD" => spread := 8.6
        "NZDCAD" => spread := 4.2
        "NZDCHF" => spread := 3.3
        "NZDJPY" => spread := 2.2
        "CHFJPY" => spread := 3.3
    spread
leverage_chart(p) =>
    float lev = 0.0
    switch p
        // MAJOR
        "AUDUSD" => lev := 33.3
        "EURUSD" => lev := 50.0
        "GBPUSD" => lev := 20.0
        "NZDUSD" => lev := 33.3
        "USDCAD" => lev := 50.0
        "USDCHF" => lev := 33.3
        "USDJPY" => lev := 20.0
        // MINOR
        "AUDCAD" => lev := 33.3
        "AUDCHF" => lev := 33.3
        "AUDJPY" => lev := 20.0
        "AUDNZD" => lev := 33.3
        "CADCHF" => lev := 33.3
        "CADJPY" => lev := 20.0
        "EURAUD" => lev := 33.3
        "EURCAD" => lev := 50.0
        "EURCHF" => lev := 33.3
        "EURGBP" => lev := 20.0
        "EURJPY" => lev := 20.0
        "EURNZD" => lev := 33.3
        "GBPAUD" => lev := 20.0
        "GBPCAD" => lev := 20.0
        "GBPCHF" => lev := 20.0
        "GBPJPY" => lev := 20.0
        "GBPNZD" => lev := 20.0
        "NZDCAD" => lev := 33.3
        "NZDCHF" => lev := 33.3
        "NZDJPY" => lev := 20.0
        "CHFJPY" => lev := 20.0
    lev
len = input.int(title="length", defval=65, minval=1, group = "Volatility")
percent = input.float(title="percent", defval = 1,minval=0, group = "Volatility")


account_balance = input.float(title ="Account Balance", defval = 2500,minval=0,group = "Trade")
allow_percent = input.float(title = "Percent of Margin",defval = 0.3,minval=0,group = "Trade")
number_of_trade = input.float(title = "Number of Trade Allowed",defval = 3,minval=0, group = "Trade")
custom_lev = input.bool(false, title = "Custom Leverage",group = "Trade")
leverage = input.float(title = "Leverage", defval = 20 ,group = "Trade")
lot = input.float(title="lot", defval = 1,minval=0, group = "Profit")
pip = input.float(title="pip", defval = 1,minval=0, group ="Profit")
account_currency = input.string(title = "Account Currency", defval = "USD", options=["USD", "EUR", "GBP", "AUD", "CAD", "CHF", "JPY", "NZD","KRW"],group ="Profit")

isForex = syminfo.type == "forex"
col = color.gray
if isForex
	col := color.black

symbol = syminfo.ticker
upperSymbol = str.upper(symbol)

mul = 1

if isForex
	mul := 10000
	if (syminfo.currency == "JPY" or syminfo.currency == "KRW")
    	mul := 100

//양봉일때 아랫꼬리 길이와 음봉일때 윗꼬리의 길이의 누적합을 평균내는 것.
//양봉일때 아랫꼬리 길이 평균, 음봉일때 윗꼬리 길이 평균도 같이 구함.
calc_volatility_wick(length) =>
    sum = 0.0
    long_sum = 0.0
    short_sum = 0.0

    long_length = 0.0
    short_length = 0.0

    for i = 1 to length
        current_close = close[i]
        current_open = open[i]
        current_low = low[i]
        current_high = high[i]

        //양봉일때
        if current_close > current_open
            sum := sum + math.abs(current_open - current_low)
            long_sum += math.abs(current_open - current_low)
            long_length += 1.0
        //음봉일때
        else
            sum := sum + math.abs(current_high - current_open)
            short_sum += math.abs(current_high - current_open)
            short_length += 1.0

    [sum/length*mul, long_sum/long_length*mul, short_sum/short_length*mul]

calc_volatility_tr(length) =>
    atr_daily = ta.ema(ta.tr(true), len)
    long_sum = 0.0
    short_sum = 0.0

    long_length = 0.0
    short_length = 0.0
    for i = 1 to length
        if close[i] > open[i]
            long_sum += math.max(high[i] - low[i], math.abs(high[i] - close[i-1]), math.abs(low[i] - close[i-1]))
            long_length += 1.0
        else
            short_sum += math.max(high[i] - low[i], math.abs(high[i] - close[i-1]), math.abs(low[i] - close[i-1]))
            short_length += 1.0
    [atr_daily*mul,long_sum/long_length*mul, short_sum/short_length*mul]

//꼬리 핍
[r_range,r_longs,r_shorts] =  calc_volatility_wick(len)
plot(r_longs*percent, title = "Range Lower wick", color=color.teal, linewidth = 1, display = display.none)
plot(r_shorts*percent, title = "Range Upper wick", color=color.red, linewidth = 1, display = display.none)
plot(r_range*percent, title = "Range Lower + Upper", color=color.navy, linewidth = 1, display = display.none)

//하루 변동 핍
[atr_daily,atr_longs,atr_shorts] =  calc_volatility_tr(len)
plot(atr_daily*percent, title = "ATR daily", color=color.navy, linewidth = 2 ,display = display.none)
plot(atr_longs*percent, title = "ATR longs", color=color.teal, linewidth = 2 ,display = display.none)
plot(atr_shorts*percent, title = "ATR shorts", color=color.red, linewidth = 2,display = display.none)


//Profit
current_pair = syminfo.ticker
is_asia_pair(pair) =>
    ret = str.contains(pair, "JPY") or str.contains(pair, "KRW")
get_pip_size(pair) =>
    is_asia_pair(pair) ? 0.01 : 0.0001
get_contract_size(pair) =>
    100000.0  // Standard lot size 100,000
get_base_currency(pair) =>
    str.substring(pair, 0, 3)
get_quote_currency(pair) =>
    str.substring(pair, 3, 6)
get_exchange_rate(from_currency, to_currency) =>
    if from_currency == to_currency
        1.0
    else
        rate_symbol = from_currency + to_currency
        request.security(rate_symbol, "1D", close, ignore_invalid_symbol=true)
calculate_pip_value(pair, lot_size_param, account_curr) =>
    base_curr = get_base_currency(pair)
    quote_curr = get_quote_currency(pair)
    pip_size = get_pip_size(pair)
    contract_size = get_contract_size(pair)
    
    pip_value_quote = pip_size * contract_size * lot_size_param
    
    if quote_curr == account_curr
        pip_value_quote
    else
        conversion_rate = get_exchange_rate(quote_curr, account_curr)
        if na(conversion_rate) or conversion_rate == 0
            inverse_rate = get_exchange_rate(account_curr, quote_curr)
            if na(inverse_rate) or inverse_rate == 0
                pip_value_quote 
            else
                pip_value_quote / inverse_rate
        else
            pip_value_quote * conversion_rate

//Trade

calc_unit(unit) =>
    base_curr = get_base_currency(current_pair)
    ret = unit*get_exchange_rate(base_curr,account_currency)
    ret
margin_allowed = account_balance*allow_percent
one_trade = margin_allowed/number_of_trade
if not custom_lev
    leverage := leverage_chart(current_pair)

to_unit = one_trade*leverage
to_base_unit = calc_unit(to_unit)

current_pip_value = calculate_pip_value(current_pair, lot, account_currency)
one_lot_profit = calculate_pip_value(current_pair, 1, account_currency)
profit_value_of_trade = calculate_pip_value(current_pair, to_base_unit/100000, account_currency)
var table table_ = table.new(position.top_right, 8, 2, frame_width=1, frame_color=color.black,border_width = 1,border_color = color.black)
if barstate.islast
    //profit
    table.cell(table_, 0, 0, str.tostring(to_unit, "#.##") + " " + account_currency, text_halign = text.align_right, text_color = color.white, bgcolor = color.gray)  
    table.cell(table_, 1, 0, str.tostring(to_base_unit, "#.##") + " unit", text_halign = text.align_right, text_color = color.white, bgcolor = color.gray)     
    table.cell(table_, 2, 0, str.tostring(leverage, "#.##")+" lev", text_halign = text.align_right, text_color = color.white, bgcolor = color.gray)     
    table.cell(table_, 3, 0, str.tostring(profit_value_of_trade*atr_daily*percent, "#.##")  + " " + account_currency, text_halign = text.align_right, text_color = color.white, bgcolor = color.gray)     
    table.cell(table_, 4, 0, str.tostring(one_lot_profit, "#.##") + " " + account_currency, text_halign = text.align_right, text_color = color.white, bgcolor = color.rgb(90, 90, 90))     
    table.cell(table_, 5, 0, str.tostring(current_pip_value*pip, "#.##") + " " + account_currency, text_halign = text.align_right, text_color = color.white, bgcolor = color.rgb(90, 90, 90))     
    table.cell(table_, 6, 0, str.tostring(spread_chart(upperSymbol), "#.##")+" pip", text_halign = text.align_right, text_color = color.white, bgcolor = color.rgb(90, 90, 90))

    //volatility
    table.cell(table_, 0, 1, str.tostring(atr_daily*percent, "#.#"), text_halign = text.align_right, text_color = color.white, bgcolor = color.navy)
    table.cell(table_, 1, 1, str.tostring(atr_longs*percent, "#.#"), text_halign = text.align_right, text_color = color.white, bgcolor = color.teal)
    table.cell(table_, 2, 1, str.tostring(atr_shorts*percent, "#.#"), text_halign = text.align_right, text_color = color.white, bgcolor = color.red)
    
    table.cell(table_, 3, 1, str.tostring(r_range*percent, "#.#"), text_halign = text.align_right, text_color = color.white, bgcolor = color.rgb(49, 27, 146, 40))
    table.cell(table_, 4, 1, str.tostring(r_longs*percent, "#.#"), text_halign = text.align_right, text_color = color.white, bgcolor = color.rgb(0, 137, 123, 40))
    table.cell(table_, 5, 1, str.tostring(r_shorts*percent, "#.#"), text_halign = text.align_right, text_color = color.white, bgcolor = color.rgb(255, 82, 82, 40))
    table.cell(table_, 6, 1, str.tostring((close-open)*mul, "#.#"), text_halign = text.align_right, text_color = color.white, bgcolor = color.black)



