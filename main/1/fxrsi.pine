//WOOJIN CHOI, 10/05/2025
//@version=6
indicator("Forex Relative Strength Index", shorttitle = "FXRSI", overlay=false)

prefix = input.string("OANDA", title ="FX source", options = ["OANDA", "TASTYFX"])

lengthRSI = input(10, title="RSI Length",group= "Settings")
lengthFast = input.int(5,  title="Fast Length",group= "Settings")  
lengthSlow = input.int(21, title="Slow Length",group= "Settings")
lengthSignal = input.int(8,title="Signal Length",group= "Settings")
lengthZscore = input.int(50, "Z-score Length",group= "Settings")
lengthSlope = input.int(2, "Slope Length",group= "Settings")

show_all = input.bool(true, "Show All", group="Display")
show_table = input.bool(true, "Show Table",  group="Display")
apply_zero_sum = input.bool(false, "Apply Zero-sum", group = "Display")

color_usd = color(#0717ff)
color_eur = color(#4dd0e1)
color_gbp = color(#9C27B0)
color_jpy = color(#FF9800)
color_cad = color(#ff0000)
color_aud = color(#04dd74)
color_nzd = color(#22ab94)
color_chf = color(#E040FB)

color_usd2 = color.new(#0717ff,1)
color_eur2 = color.new(#4dd0e1,1)
color_gbp2 = color.new(#9C27B0,1)
color_jpy2 = color.new(#FF9800,1)
color_cad2 = color.new(#ff0000,1)
color_aud2 = color.new(#04dd74,1)
color_nzd2 = color.new(#22ab94,1)
color_chf2 = color.new(#E040FB,1)

eurusd = request.security(prefix + ":eurusd", timeframe.period, close)
eurgbp = request.security(prefix + ":eurgbp", timeframe.period, close)
eurchf = request.security(prefix + ":eurchf", timeframe.period, close)
eurjpy = request.security(prefix + ":eurjpy", timeframe.period, close) / 100
euraud = request.security(prefix + ":euraud", timeframe.period, close)
eurcad = request.security(prefix + ":eurcad", timeframe.period, close)
eurnzd = request.security(prefix + ":eurnzd", timeframe.period, close)
usdchf = request.security(prefix + ":usdchf", timeframe.period, close)
usdjpy = request.security(prefix + ":usdjpy", timeframe.period, close) / 100
usdcad = request.security(prefix + ":usdcad", timeframe.period, close)
gbpusd = request.security(prefix + ":gbpusd", timeframe.period, close)
gbpchf = request.security(prefix + ":gbpchf", timeframe.period, close)
gbpjpy = request.security(prefix + ":gbpjpy", timeframe.period, close) / 100
gbpaud = request.security(prefix + ":gbpaud", timeframe.period, close)
gbpcad = request.security(prefix + ":gbpcad", timeframe.period, close)
gbpnzd = request.security(prefix + ":gbpnzd", timeframe.period, close)
chfjpy = request.security(prefix + ":chfjpy", timeframe.period, close) / 100
audusd = request.security(prefix + ":audusd", timeframe.period, close)
audchf = request.security(prefix + ":audchf", timeframe.period, close)
audjpy = request.security(prefix + ":audjpy", timeframe.period, close) / 100
audcad = request.security(prefix + ":audcad", timeframe.period, close)
audnzd = request.security(prefix + ":audnzd", timeframe.period, close)
cadchf = request.security(prefix + ":cadchf", timeframe.period, close)
cadjpy = request.security(prefix + ":cadjpy", timeframe.period, close) / 100
nzdusd = request.security(prefix + ":nzdusd", timeframe.period, close)
nzdchf = request.security(prefix + ":nzdchf", timeframe.period, close)
nzdjpy = request.security(prefix + ":nzdjpy", timeframe.period, close) / 100
nzdcad = request.security(prefix + ":nzdcad", timeframe.period, close)

usd = (-eurusd * 0.40 - gbpusd * 0.20 + usdjpy * 0.25 + usdchf * 0.05 - audusd * 0.07 + usdcad * 0.02 - nzdusd * 0.01)
eur = ( eurusd * 0.50 + eurjpy * 0.20 + eurgbp * 0.15 + eurchf * 0.05 + euraud * 0.05 + eurcad * 0.03 + eurnzd * 0.02)
gbp = (-eurgbp * 0.12 + gbpusd * 0.45 + gbpjpy * 0.20 + gbpchf * 0.08 + gbpaud * 0.08 + gbpcad * 0.04 + gbpnzd * 0.03)
jpy = (-eurjpy * 0.20 - usdjpy * 0.40 - gbpjpy * 0.20 - audjpy * 0.10 - cadjpy * 0.05 - chfjpy * 0.03 - nzdjpy * 0.02)
cad = (-eurcad * 0.05 - usdcad * 0.58 - gbpcad * 0.08 + cadchf * 0.05 + cadjpy * 0.08 - audcad * 0.10 - nzdcad * 0.06)
aud = (-euraud * 0.08 + audusd * 0.52 - gbpaud * 0.15 + audchf * 0.05 + audjpy * 0.10 + audcad * 0.06 + audnzd * 0.04)
nzd = (-eurnzd * 0.03 + nzdusd * 0.58 - gbpnzd * 0.08 + nzdchf * 0.05 + nzdjpy * 0.08 - audnzd * 0.12 + nzdcad * 0.06)
chf = (-eurchf * 0.25 - usdchf * 0.40 - gbpchf * 0.15 + chfjpy * 0.08 - audchf * 0.05 - cadchf * 0.04 - nzdchf * 0.03)

zscore(src, length) =>
    mean = ta.ema(src, length)
    stddev = ta.stdev(src, length)
    (src - mean) / stddev

rsi_macd_calc(src, fast, slow, sig) =>
    fast_ma = ta.ema(src, fast)
    slow_ma = ta.ema(src, slow)
    macd = zscore(fast_ma - slow_ma,lengthZscore)
    signal = ta.ema(macd, sig)
    rsi_val = ta.rsi(signal, lengthRSI)
    rsi_val

rsi_usd = rsi_macd_calc(usd, lengthFast, lengthSlow, lengthSignal)
rsi_eur = rsi_macd_calc(eur, lengthFast, lengthSlow, lengthSignal)
rsi_gbp = rsi_macd_calc(gbp, lengthFast, lengthSlow, lengthSignal)
rsi_jpy = rsi_macd_calc(jpy, lengthFast, lengthSlow, lengthSignal)
rsi_cad = rsi_macd_calc(cad, lengthFast, lengthSlow, lengthSignal)
rsi_aud = rsi_macd_calc(aud, lengthFast, lengthSlow, lengthSignal)
rsi_nzd = rsi_macd_calc(nzd, lengthFast, lengthSlow, lengthSignal)
rsi_chf = rsi_macd_calc(chf, lengthFast, lengthSlow, lengthSignal)

if apply_zero_sum
    meanRSI = (rsi_eur + rsi_usd + rsi_gbp + rsi_chf + rsi_jpy + rsi_aud + rsi_cad + rsi_nzd)/8
    rsi_eur := rsi_eur - meanRSI + 50
    rsi_usd := rsi_usd - meanRSI + 50
    rsi_gbp := rsi_gbp - meanRSI + 50
    rsi_chf := rsi_chf - meanRSI + 50
    rsi_jpy := rsi_jpy - meanRSI + 50
    rsi_aud := rsi_aud - meanRSI + 50
    rsi_cad := rsi_cad - meanRSI + 50
    rsi_nzd := rsi_nzd - meanRSI + 50

symbol = syminfo.ticker
upperSymbol = str.upper(symbol)

showUSD = str.contains(upperSymbol, "USD")
showEUR = str.contains(upperSymbol, "EUR")
showGBP = str.contains(upperSymbol, "GBP")
showJPY = str.contains(upperSymbol, "JPY")
showCAD = str.contains(upperSymbol, "CAD")
showAUD = str.contains(upperSymbol, "AUD")
showNZD = str.contains(upperSymbol, "NZD")
showCHF = str.contains(upperSymbol, "CHF")

usd_ma = ta.ema(rsi_usd,10)
eur_ma = ta.ema(rsi_eur,10)
gbp_ma = ta.ema(rsi_gbp,10)
jpy_ma = ta.ema(rsi_jpy,10)
cad_ma = ta.ema(rsi_cad,10)
aud_ma = ta.ema(rsi_aud,10)
nzd_ma = ta.ema(rsi_nzd,10)
chf_ma = ta.ema(rsi_chf,10)

slope_usd = (rsi_usd - rsi_usd[lengthSlope])
slope_eur = (rsi_eur - rsi_eur[lengthSlope])
slope_gbp = (rsi_gbp - rsi_gbp[lengthSlope])
slope_jpy = (rsi_jpy - rsi_jpy[lengthSlope])
slope_cad = (rsi_cad - rsi_cad[lengthSlope])
slope_aud = (rsi_aud - rsi_aud[lengthSlope])
slope_nzd = (rsi_nzd - rsi_nzd[lengthSlope])
slope_chf = (rsi_chf - rsi_chf[lengthSlope])

plot(showUSD or show_all ? rsi_usd : na, title="USD", color= (usd_ma < rsi_usd and slope_usd > 0) or (usd_ma > rsi_usd and slope_usd < 0) ? color_usd : color_usd2, linewidth=1)
plot(showEUR or show_all ? rsi_eur : na, title="EUR", color= (eur_ma < rsi_eur and slope_eur > 0) or (eur_ma > rsi_eur and slope_eur < 0) ? color_eur : color_eur2, linewidth=1)
plot(showGBP or show_all ? rsi_gbp : na, title="GBP", color= (gbp_ma < rsi_gbp and slope_gbp > 0) or (gbp_ma > rsi_gbp and slope_gbp < 0) ? color_gbp : color_gbp2, linewidth=1)
plot(showJPY or show_all ? rsi_jpy : na, title="JPY", color= (jpy_ma < rsi_jpy and slope_jpy > 0) or (jpy_ma > rsi_jpy and slope_jpy < 0) ? color_jpy : color_jpy2, linewidth=1)
plot(showCAD or show_all ? rsi_cad : na, title="CAD", color= (cad_ma < rsi_cad and slope_cad > 0) or (cad_ma > rsi_cad and slope_cad < 0) ? color_cad : color_cad2, linewidth=1)
plot(showAUD or show_all ? rsi_aud : na, title="AUD", color= (aud_ma < rsi_aud and slope_aud > 0) or (aud_ma > rsi_aud and slope_aud < 0) ? color_aud : color_aud2, linewidth=1)
plot(showNZD or show_all ? rsi_nzd : na, title="NZD", color= (nzd_ma < rsi_nzd and slope_nzd > 0) or (nzd_ma > rsi_nzd and slope_nzd < 0) ? color_nzd : color_nzd2, linewidth=1)
plot(showCHF or show_all ? rsi_chf : na, title="CHF", color= (chf_ma < rsi_chf and slope_chf > 0) or (chf_ma > rsi_chf and slope_chf < 0) ? color_chf : color_chf2, linewidth=1)

//plot(rsi_eur-50+rsi_usd-50+rsi_gbp-50+rsi_chf-50+rsi_jpy-50+rsi_aud-50+rsi_cad-50+rsi_nzd-50)

zero = hline(50, "zero", color=color.rgb(0, 0, 0), linestyle=hline.style_dotted)
u1 = hline(90, "upper 1", color=color.rgb(0, 0, 0), linestyle=hline.style_dotted)
l1 =hline(10, "lower 1", color=color.rgb(0, 0, 0), linestyle=hline.style_dotted)
u2 = hline(100, "upper 2", color=color.rgb(0, 0, 0), linestyle=hline.style_solid)
l2 = hline(0, "lower 2", color=color.rgb(0, 0, 0), linestyle=hline.style_solid)
fill(u1,u2, title="top fill", color=color.new(#000000, 93))
fill(l1, l2, title="bot fill", color=color.new(#000000, 93))

if show_table and barstate.islast
    var table ranking_table = table.new(position.middle_right, 3, 8, bgcolor=color.white, border_width=1, border_color=color.black)

    currencies = array.new<float>()
    currency_names = array.new<string>()
    currency_colors = array.new<color>()
    slope_values = array.new<float>()

    array.push(currencies, rsi_eur)
    array.push(currency_names, "EUR")
    array.push(slope_values, slope_eur)
    array.push(currency_colors, color_eur)
    
    array.push(currencies, rsi_usd)
    array.push(currency_names, "USD")
    array.push(slope_values, slope_usd)    
    array.push(currency_colors, color_usd)
    
    array.push(currencies, rsi_gbp)
    array.push(currency_names, "GBP")
    array.push(slope_values, slope_gbp)
    array.push(currency_colors, color_gbp)
    
    array.push(currencies, rsi_jpy)
    array.push(currency_names, "JPY")
    array.push(slope_values, slope_jpy)
    array.push(currency_colors, color_jpy)
    
    array.push(currencies, rsi_chf)
    array.push(currency_names, "CHF")
    array.push(slope_values, slope_chf)
    array.push(currency_colors, color_chf)
    
    array.push(currencies, rsi_aud)
    array.push(currency_names, "AUD")
    array.push(slope_values, slope_aud)
    array.push(currency_colors, color_aud)
    
    array.push(currencies, rsi_cad)
    array.push(currency_names, "CAD")
    array.push(slope_values, slope_cad)
    array.push(currency_colors, color_cad)
    
    array.push(currencies, rsi_nzd)
    array.push(currency_names, "NZD")
    array.push(slope_values, slope_nzd)
    array.push(currency_colors, color_nzd)
    
    // 정렬
    for i = 0 to array.size(slope_values) - 2
        for j = i + 1 to array.size(slope_values) - 1
            if array.get(slope_values, i) < array.get(slope_values, j)
                temp_rsi = array.get(currencies, i)
                temp_name = array.get(currency_names, i)
                temp_slope = array.get(slope_values,i)
                temp_color = array.get(currency_colors, i)
                
                array.set(currencies, i, array.get(currencies, j))
                array.set(currency_names, i, array.get(currency_names, j))
                array.set(slope_values, i, array.get(slope_values, j))
                array.set(currency_colors, i, array.get(currency_colors, j))
                
                array.set(currencies, j, temp_rsi)
                array.set(currency_names, j, temp_name)
                array.set(slope_values, j, temp_slope)
                array.set(currency_colors, j, temp_color)

    for i = 0 to array.size(currencies) - 1
        name = array.get(currency_names, i)
        rsi_val = array.get(currencies, i)
        slope_value = array.get(slope_values, i)
        curr_color = array.get(currency_colors, i)
        
        rsi_col = rsi_val > 50 ? color.green : rsi_val < 50 ? color.red : color.black
        slope_col = slope_value > 0 ? color.green : slope_value < 0 ? color.red : color.black

        table.cell(ranking_table, 0, i, name, text_color=curr_color, text_size=size.tiny)
        table.cell(ranking_table, 1, i, str.tostring(rsi_val, "#.##"), text_color=rsi_col, text_size=size.tiny)
        table.cell(ranking_table, 2, i, str.tostring(slope_value, "#.##"), text_color=slope_col, text_size=size.tiny)