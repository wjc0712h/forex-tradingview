//WOOJIN CHOI, 10/18/2025
//@version=6
indicator("FX sessions",shorttitle="FXSESSION", overlay=true, max_labels_count=500, max_boxes_count = 500, max_lines_count = 500)

groupd_     = "Sessions" + "  UTC-7"
tk          = "Tokyo" + "  Time:"
lo          = "London" + "  Time:"
ny          = "New York" + "  Time:"
si          = "Sydney" + "  Time:"

// ＩＮＰＵＴＳ
display_mid_range = input.bool(false, "Middle Range", group = "Range")
extend            = input.bool(false, "Future Range", group = "Range")
show_label            = input.bool(false, "show_label", group = "Range")

show_tokyo          = input.bool(true, tk, inline = "Tokyo", group = groupd_)
tk_ss               = input.session("1700-0200", "", inline = "Tokyo", group = groupd_)
tokyo_color         = input.color(color.red, "", inline = "Tokyo", group = groupd_)

show_london         = input.bool(true, lo, inline = "London", group = groupd_)
lo_ss               = input.session("0000-0900", "", inline = "London", group = groupd_)
london_color        = input.color(color.green, "", inline = "London", group = groupd_)

show_ny             = input.bool(true, ny, inline = "NY", group = groupd_)
ny_ss               = input.session("0500-1400", "", inline = "NY", group = groupd_)
ny_color            = input.color(color.blue, "", inline = "NY", group = groupd_)

show_si             = input.bool(false, si, inline = "Sidney", group = groupd_)
si_ss               = input.session("1400-2300", "", inline = "Sidney", group = groupd_)
si_color            = input.color(color.purple, "", inline = "Sidney", group = groupd_)

open_ = request.security(ticker.standard(syminfo.tickerid), timeframe.period, open)
close_ = request.security(ticker.standard(syminfo.tickerid), timeframe.period, close)
high_ = request.security(ticker.standard(syminfo.tickerid), timeframe.period, high)
low_ = request.security(ticker.standard(syminfo.tickerid), timeframe.period, low)
formatSessionString(sessionStr) =>
    // Extract start and end times
    startStr = str.substring(sessionStr, 0, 4)
    endStr   = str.substring(sessionStr, 5, 9)
    // Insert colons
    formattedStart = str.format("{0}:{1}", str.substring(startStr, 0, 2), str.substring(startStr, 2, 4))
    formattedEnd   = str.format("{0}:{1}", str.substring(endStr, 0, 2), str.substring(endStr, 2, 4))
    // Combine into final formatted string
    str.format("{0}-{1}", formattedStart, formattedEnd)


in_session(i_sess) =>
    t = time(timeframe.period, i_sess + ":23456", "UTC-7")
    time == t

box_session(session, sessionss, display, color)=>
    sessions   = in_session(sessionss)
    var boxx = box(na)
    var mid = line(na)

    // New: extended midline across session gap
    var line ext_mid = na
    var line ext_h = na
    var line ext_l = na

    var labl = label(na)

    var highh = array.new<float>()
    var loww = array.new<float>()
    var vol = array.new<float>()
    var delta = array.new<float>()


    // When session changes (start of new session)
    if sessions and not sessions[1]

        // Clear arrays for new session
        highh.clear()
        loww.clear()
        vol.clear()
        delta.clear()

        // New box and midline
        boxx := box.new(bar_index, na, na, na
                         , bgcolor = color.new(color, 90)
                         , border_color = color.new(color, 50)
                         )

        if display_mid_range
            mid := line.new(bar_index, na, na, na, color = color.new(color, 30))
        
        labl := label.new(bar_index, na
                         , style = label.style_label_upper_left
                         , color = color(na)
                         , textcolor = chart.fg_color
                         , size = size.small
                         , textalign = text.align_left
                         )


    // During session, update everything
    if display and sessions  
   
        highh.push(high_)
        loww.push(low_)
        vol.push(volume)
        delta.push(close_ > open_ ? volume : -volume)

        h = array.max(highh)
        l = array.min(loww)

        boxx.set_right(bar_index+1)
        boxx.set_top(h)
        boxx.set_bottom(l)

        mid_y = (h + l) / 2
        data = session + ": " + formatSessionString(sessionss) + "\n\n" + "Volume:  " + str.tostring(array.sum(vol), format.volume) + "\n" + "Delta:     " + str.tostring(array.sum(delta), format.volume)

        if display_mid_range
            mid.set_x2(bar_index+1)
            mid.set_y1(mid_y)
            mid.set_y2(mid_y)

        if show_label
            labl.set_y(l)
            labl.set_text(data)


    if extend  
        if not sessions and sessions[1] 
            h = array.max(highh)
            l = array.min(loww)

            mid_y = (h + l) / 2
            // ext_mid := line.new(bar_index, mid_y, bar_index, mid_y, style=line.style_dashed, color=color.new(color, 30))
            ext_h := line.new(bar_index, h, bar_index, h, style=line.style_solid, color=color.new(color, 30))
            ext_l := line.new(bar_index, l, bar_index, l, style=line.style_solid, color=color.new(color, 30))

        if not sessions
            ext_mid.set_x2(bar_index)
            ext_h.set_x2(bar_index)
            ext_l.set_x2(bar_index)



box_session("Tokyo", tk_ss, show_tokyo, tokyo_color)
box_session("London", lo_ss, show_london, london_color)
box_session("New York", ny_ss, show_ny, ny_color)
box_session("Sydney", si_ss, show_si, si_color)


