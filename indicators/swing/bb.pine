//@version=6
indicator(shorttitle="BB", title="Swing Bollinger Bands", overlay=true, timeframe="", timeframe_gaps=true)

length = input.int(20, minval=1,  group="BB Settings")
maType = input.string("SMA", "Basis MA Type", options = ["SMA", "EMA", "SMMA (RMA)", "WMA", "VWMA"], group="BB Settings")
src = input(close, title="Source", group="BB Settings")
mult = input.float(2.0, minval=0.001, maxval=50, title="StdDev", group="BB Settings")

ma(source, length, _type) =>
    switch _type
        "SMA" => ta.sma(source, length)
        "EMA" => ta.ema(source, length)
        "SMMA (RMA)" => ta.rma(source, length)
        "WMA" => ta.wma(source, length)
        "VWMA" => ta.vwma(source, length)

basis = ma(src, length, maType)
dev = mult * ta.stdev(src, length)
upper = basis + dev
lower = basis - dev
offset = input.int(0, "Offset", minval = -500, maxval = 500, display = display.data_window, group="BB Settings")
plot(basis, "Basis", color=color.new(color.blue, 66), offset = offset)
p1 = plot(upper, "Upper", color.new(color.blue, 66), offset = offset)
p2 = plot(lower, "Lower", color.new(color.blue, 66), offset = offset)

//SQUEEZE
bb_width = (upper - lower) / basis
sqz_len = input.int(20, "BB Width MA Length", minval=1, group="Squeeze Settings")

bb_squeeze = bb_width < (ta.ema(bb_width, sqz_len) * input.float(0.6, "Squeeze Threshold(%)", minval=0.1, step=0.1, group="Squeeze Settings"))
squeeze_color = bb_squeeze ? color.new(color.blue,66) : na
fill(p1, p2, title="Squeeze Background", color=squeeze_color)

//SIGNAL
long_signal = open < lower and close > lower
short_signal = open > upper and close < upper 

//PLOT
plotshape(long_signal, title="BB Long", location=location.belowbar, style=shape.triangleup, color=color.rgb(33, 149, 243, 33), size=size.tiny)
plotshape(short_signal, title="BB Short", location=location.abovebar, style=shape.triangledown, color=color.rgb(33, 149, 243, 33), size=size.tiny)

//ALERT
alertcondition(long_signal or short_signal, title="Swing BB Signal", message="SWING BB Signal")
alertcondition(long_signal, title="SWING BB LONG ALERT", message="SWING BB LONG Signal")
alertcondition(short_signal, title="SWING BB SHORT ALERT", message="SWING BB SHORT Signal")