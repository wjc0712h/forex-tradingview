//@version=6
indicator("Double Bollinger Bands", shorttitle="DBB", overlay=true)

const string session_group = "Trading Session"
const string main_group = "Main Bollinger Bands"
const string sub_group = "Sub Bollinger Bands"
const string signal_group = "Signals"

// === Trading Session ===
session = input.session("24/7", "Trading Session", options=["london-newyork", "sidney-tokyo", "24/7"], group=session_group,display=display.all-display.status_line)
trading_session = session == "london-newyork" ? "0300-1500" :
                  session == "sidney-tokyo" ? "1700-0500" : 
                  session == "24/7" ? "0000-0000" :
                  session
t = time(timeframe.period, trading_session)
bgcolor(title = "session", color = time == t ? color.rgb(190, 190, 190, 87) : na, display = display.none)

hf_timeframe = input.timeframe("240", title="DI Timeframe")
di_len = input.int(72, "EMA Length")
di_min = input.float(0.03, "DI min")
di_max = input.float(3, "DI max")

// === Function for Moving Average ===
ma(source, length, _type) =>
    switch _type
        "SMA" => ta.sma(source, length)
        "EMA" => ta.ema(source, length)
        "SMMA(RMA)" => ta.rma(source, length)
        "WMA" => ta.wma(source, length)
        "VWMA" => ta.vwma(source, length)

// === Main Bollinger Bands ===
length = input.int(20, minval=1, title="Length", group=main_group,display=display.all-display.status_line)
maType = input.string("EMA", "Basis MA Type", options=["SMA", "EMA", "SMMA(RMA)", "WMA", "VWMA"], group=main_group,display=display.all-display.status_line)
src = input.source(close, title="Source", group=main_group,display=display.all-display.status_line)
mult = input.float(2.0, minval=0.001, maxval=50, title="StdDev", group=main_group,display=display.all-display.status_line)
main_tf = input.string("Chart", title="Time Frame", options=["Chart", "1min", "3min", "5min","6min","9min", "10min", "15min", "30min","90min", "1hr", "4hr", "D"], group=main_group,display=display.all-display.status_line)
offset = input.int(0, "Offset", minval=-500, maxval=500, group=main_group,display=display.all-display.status_line)

main_tf_res = main_tf == "Chart" ? timeframe.period :
              main_tf == "1min" ? "1" :
              main_tf == "3min" ? "3" :
              main_tf == "5min" ? "5" :
              main_tf == "6min" ? "6" :
              main_tf == "9min" ? "9" :
              main_tf == "10min" ? "10" :
              main_tf == "15min" ? "15" :
              main_tf == "30min" ? "30" :
              main_tf == "90min" ? "90" :
              main_tf == "1hr" ? "60" :
              main_tf == "4hr" ? "240" :
              main_tf == "D" ? "D" :
              main_tf

basis = request.security(syminfo.tickerid, main_tf_res, ma(src, length, maType),lookahead=barmerge.lookahead_off)
dev = mult * request.security(syminfo.tickerid, main_tf_res, ta.stdev(src, length),lookahead=barmerge.lookahead_off)
upper = basis + dev
lower = basis - dev

plot(basis, "Main Basis", color=color.blue, offset=offset,linewidth = 2, display = display.none)
p1 = plot(upper, "Main Upper", color=color.rgb(33, 149, 243, 70), offset=offset,linewidth = 2)
p2 = plot(lower, "Main Lower", color=color.rgb(33, 149, 243, 70), offset=offset,linewidth = 2)

// === Sub Bollinger Bands ===
length_ = input.int(3, minval=1, title="Length", group=sub_group,display=display.all-display.status_line)
maType_ = input.string("EMA", "Basis MA Type", options=["SMA", "EMA", "SMMA(RMA)", "WMA", "VWMA"], group=sub_group,display=display.all-display.status_line)
src_ = input.source(open, title="Source", group=sub_group,display=display.all-display.status_line)
mult_ = input.float(4.0, minval=0.001, maxval=50, title="StdDev", group=sub_group,display=display.all-display.status_line)
sub_tf = input.string("Chart", title="Time Frame", options=["Chart", "1min","3min", "5min","6min","9min","10min", "15min", "30min","90min", "1hr", "4hr", "D"], group=sub_group,display=display.all-display.status_line)
offset_ = input.int(0, "Sub Offset", minval=-500, maxval=500, group=sub_group,display=display.all-display.status_line)

sub_tf_res = sub_tf == "Chart" ? timeframe.period :
             sub_tf == "1min" ? "1" :
             sub_tf == "3min" ? "3" :
             sub_tf == "5min" ? "5" :
             sub_tf == "6min" ? "6" :
             sub_tf == "9min" ? "9" :
             sub_tf == "10min" ? "10" :
             sub_tf == "15min" ? "15" :
             sub_tf == "30min" ? "30" :
             sub_tf == "90min" ? "90" :
             sub_tf == "1hr" ? "60" :
             sub_tf == "4hr" ? "240" :
             sub_tf == "D" ? "D" :
             sub_tf

basis_ = request.security(syminfo.tickerid, sub_tf_res, ma(src_, length_, maType_),lookahead=barmerge.lookahead_off)
dev_ = mult_ * request.security(syminfo.tickerid, sub_tf_res, ta.stdev(src_, length_),lookahead=barmerge.lookahead_off)
upper_ = basis_ + dev_
lower_ = basis_ - dev_

plot(basis_, "Sub Basis", color=#F23645, offset=offset_, display = display.none)
p1_ = plot(upper_, "Sub Upper", color=color.rgb(242, 54, 70, 70), offset=offset_)
p2_ = plot(lower_, "Sub Lower", color=color.rgb(242, 54, 70, 70), offset=offset_)

// === SIGNAL ===
signal_high = request.security(syminfo.tickerid, timeframe.period, high)
signal_low = request.security(syminfo.tickerid, timeframe.period, low)
signal_open = request.security(syminfo.tickerid, timeframe.period, open)
signal_close = request.security(syminfo.tickerid, timeframe.period, close)

main_basis_signal = request.security(syminfo.tickerid, main_tf_res, ma(src, length, maType))
main_dev_signal = mult * request.security(syminfo.tickerid, main_tf_res, ta.stdev(src, length))
main_upper_signal = main_basis_signal + main_dev_signal
main_lower_signal = main_basis_signal - main_dev_signal

sub_basis_signal = request.security(syminfo.tickerid, sub_tf_res, ma(src_, length_, maType_))
sub_dev_signal = mult_ * request.security(syminfo.tickerid, sub_tf_res, ta.stdev(src_, length_))
sub_upper_signal = sub_basis_signal + sub_dev_signal
sub_lower_signal = sub_basis_signal - sub_dev_signal

touches_upper = signal_high >= main_upper_signal and signal_high >= sub_upper_signal
touches_lower = signal_low <= main_lower_signal and signal_low <= sub_lower_signal


// squeeze
bb_width = (main_upper_signal - main_lower_signal) / main_basis_signal
bb_width_ma = ta.ema(bb_width, 100)

squeeze = bb_width < (bb_width_ma * 0.5)
squeezeColor = squeeze ? color.new(color.rgb(33, 149, 243), 80) : na
fill(p1, p2, title="Squeeze Background", color=squeezeColor)

buySignal = touches_lower and signal_close > main_lower_signal and not na(t) 
sellSignal = touches_upper and signal_close < main_upper_signal  and not na(t)

// Plot signals
plotshape(buySignal , title="Buy Signal", location=location.belowbar, style=shape.triangleup, color=color.purple, size=size.tiny)
plotshape(sellSignal, title="Sell Signal", location=location.abovebar, style=shape.triangledown, color=color.purple, size=size.tiny)

// ALERT
alertcondition(buySignal or sellSignal, title="BUY OR SELL ALERT", message="Double Bollinger Bands Signal")
alertcondition(buySignal, title="BUY ALERT", message="Double Bollinger Bands Buy Signal")
alertcondition(sellSignal, title="SELL ALERT", message="Double Bollinger Bands Sell Signal")